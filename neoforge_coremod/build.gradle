/**
 * Adapted from https://github.com/KostromDan/Crash-Assistant, LGPL-3.0
 */
plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id "me.shedaniel.unified-publishing" version "0.1.+"
}

configurations {
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

base {
    archivesName = "ConfigManager-neoforge-1.21.8-1.21.11"
}

dependencies {
    shadowBundle 'com.electronwill.night-config:toml:3.6.4'
    shadowBundle 'com.electronwill.night-config:json:3.6.4'
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier.set('dev-shadow')
    relocate 'com.electronwill.nightconfig', 'io.github.thebossmagnus.mods.config_manager.nightconfig'
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    dependsOn(':neoforge:build')
    from(project(':neoforge').tasks.jar) {
        rename { "config_manager-neoforge.jar" }
        into 'META-INF/jarjar'
    }
    from(zipTree(project(':neoforge').tasks.jar.archiveFile.get().asFile)) {
        include 'META-INF/neoforge.mods.toml'
        into ''
    }
    exclude("config_manager.mixins.json")
    exclude("config_manager.neoforge.mixins.json")
    exclude("io/github/thebossmagnus/mods/config_manager/neoforge/**")
    exclude("io/github/thebossmagnus/mods/config_manager/common/**")

}

neoForge {
    version = neoforge_version
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
        }
        data {
            clientData()
        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

unifiedPublishing {
    project {
        gameVersions = ["1.21.9", "1.21.10", "1.21.11"]
        gameLoaders = ["neoforge"]


        displayName = "Config Manager ${project.version} NeoForge"
        version = project.version
        changelog = file('../CHANGELOG.md').text
        releaseType = release_type

        mainPublication tasks.jar

        var cfToken = System.getenv("CURSEFORGE_TOKEN")
        if (cfToken != null) {
            curseforge {
                token = cfToken
                id = "1340480"
            }
        }

        var mrToken = System.getenv("MODRINTH_TOKEN")
        if (mrToken != null) {
            modrinth {
                token = mrToken
                id = "jlNms3Jp"
            }
        }
    }
}

tasks.register('publishRelease') {
    dependsOn 'publishUnified'
}
