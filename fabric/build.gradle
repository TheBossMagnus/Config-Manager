plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id "me.shedaniel.unified-publishing" version "0.1.+"
}

base {
    archivesName = "ConfigManager-${project.name}-1.21.x"
}

configurations {
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"

    shadowBundle 'com.electronwill.night-config:toml:3.6.4'
    shadowBundle 'com.electronwill.night-config:json:3.6.4'
    modImplementation "maven.modrinth:modmenu:${project.modmenu_version}"
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier.set('dev-shadow')
    relocate 'com.electronwill.nightconfig', 'io.github.thebossmagnus.mods.config_manager.nightconfig'
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

loom {
    def aw = project(':common').file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName('Fabric Client')
            ideConfigGenerated(true)
            runDir('runs/client')
        }
        server {
            server()
            setConfigName('Fabric Server')
            ideConfigGenerated(true)
            runDir('runs/server')
        }
    }
}

unifiedPublishing {
    project {
        gameVersions = ["1.21", "1.21.1", "1.21.2", "1.21.3", "1.21.4","1.21.5","1.21.6","1.21.7","1.21.8","1.21.9","1.21.10","1.21.11"]
        gameLoaders = ["fabric", "quilt"]

        displayName = "Config Manager ${project.version} Fabric"
        version = project.version
        changelog = file('../CHANGELOG.md').text
        releaseType = release_type
        relations {
            optional {
                curseforge = "modmenu"
                modrinth = "modmenu"
            }
        }

        mainPublication tasks.remapJar

        var cfToken = System.getenv("CURSEFORGE_TOKEN")
        if (cfToken != null) {
            curseforge {
                token = cfToken
                id = "1340480"
            }
        }

        var mrToken = System.getenv("MODRINTH_TOKEN")
        if (mrToken != null) {
            modrinth {
                token = mrToken
                id = "jlNms3Jp"
            }
        }
    }
}

tasks.register('publishRelease') {
    dependsOn 'publishUnified'
}